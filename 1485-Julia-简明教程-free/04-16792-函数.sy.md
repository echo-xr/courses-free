---
show: step
version: 1.0
enable_checker: false
---

# 函数

## 实验介绍

本节实验我们将学习 Julia 中的函数如何定义、参数如何设置以及流程控制。

#### 知识点

- 如何定义函数
- 函数的返回值
- 匿名函数
- 函数的参数

## 函数的定义

在 Julia 里，函数是一个将参数值元组映射到返回值的对象。Julia 的函数不是纯粹的数学函数，在某种意义上，函数可以改变并受程序的全局状态的影响。在 Julia 中定义函数的基本语法是：

```julia
julia> function f(a, b)
           return a + b
       end
f (generic function with 1 method)

julia>
```

使用关键字 function 定义函数，后面的 f 是函数名，括号内是参数，多个参数使用逗号隔开，函数体内部使用 return 关键字定义返回值，使用 end 关键字结束函数定义。

如上所示第二行缩进了 4 个字符，这不是语法要求，而是为了方便阅读。Julia 与大多数语言一样，没有强制缩进要求，缩进是为了美观和便于阅读。

此外还有一种不使用 function 关键字的方法定义函数，这是一种简化的方法：

```julia
julia> f(a, b) = a + b
f (generic function with 1 method)

julia> f(3, 8)
11

julia>
```

## 返回值

其中返回值的设置也可以省略 return 关键字，这样默认最后一行代码的值即为函数的返回值：

```julia
julia> function f(a, b)
           return a + b
       end
f (generic function with 1 method)

julia> f(3, 4)
7

julia> x = f(3, 4)
7

julia> x
7

julia> function f(a, b)
           a * b
       end
f (generic function with 1 method)

julia> f(3, 2)
6

julia>
```

同变量名一样，Unicode 字符也可以作为函数名，例如求和符号 ∑ 可以作为变量名，它的生成方式是输入 `\sum` 后按 Tab 键：

```julia
julia> function ∑(a, b)
           return a + b
       end
∑ (generic function with 1 method)

julia> ∑(3, 5)
8

julia>
```

当函数内部使用了 return 定义返回值，如果这一行代码被执行，后面的代码将不会被执行：

```julia
julia> function f(a, b)
           println("Start")
           return a + b
           println("End")
       end
f (generic function with 1 method)

julia> f(3, 4)
Start
7

julia>
```

如上所示，调用函数 f 会打印 Start 但不会打印 End 。如果不写 return 会怎样呢？返回值将是 nothing ：

```julia
julia> function f(a, b)
           println("Start")
           a + b
           println("End")
       end
f (generic function with 1 method)

julia> x = f(3, 4)
Start
End

julia> x

julia> println(x)
nothing

julia> typeof(x)
Nothing

julia>
```

函数会将最后一行代码的执行结果作为返回值，println 函数的作用就是打印内容到屏幕上，其返回值就是 nothing ，这是用来表示 “空” 的概念，类似于 Python 中的 None 、MySQL 中的 null 。

此外，可以在定义函数的时候规定返回值的数据类型，格式是在定义函数的括号后面加双冒号 `::` ，在双冒号后面写上数据类型：

```julia
julia> f(a, b) = a + b
f (generic function with 1 method)

julia> x = f(2, 3)
5

julia> typeof(x)
Int64

julia> f(a, b)::Int32 = a + b
f (generic function with 1 method)

julia> x = f(2, 3)
5

julia> typeof(x)
Int32

julia> function get_sum(start, stop)::Int32
           s = 0
           while start <= stop
               s += start
               start += 1
           end
           return s
       end
get_sum (generic function with 1 method)

julia> s = get_sum(1, 100)
5050

julia> typeof(s)
Int32

julia>
```

如果返回值不能被转换成预设数据类型，就会出现报错：

```julia
julia> function get_sum(start, stop)::Int32
           s = 0
           while start <= stop
               s += start
               start += 1
           end
           return string(s)
       end
get_sum (generic function with 1 method)

julia> get_sum(3, 9)
ERROR: MethodError: Cannot `convert` an object of type String to an object of type Int32
Closest candidates are:
  convert(::Type{T}, ::T) where T<:Number at number.jl:6
  convert(::Type{T}, ::Number) where T<:Number at number.jl:7
  convert(::Type{T}, ::Ptr) where T<:Integer at pointer.jl:23
  ...
Stacktrace:
 [1] get_sum(::Int64, ::Int64) at ./REPL[49]:7
 [2] top-level scope at REPL[50]:1

julia>
```

## 运算符作为函数

在 Julia 中比较诡异的是加减乘除等运算符本身就是内置函数：

```julia
julia> +
+ (generic function with 166 methods)

julia> +(3, 4)
7

julia> -
- (generic function with 174 methods)

julia> -(3, 4)
-1

julia> *
* (generic function with 358 methods)

julia> *(3, 4)
12

julia> /
/ (generic function with 115 methods)

julia> /(3, 4)
0.75

julia> +(1, 2, 3)
6

julia> *(3, 4, 5)
60

julia>
```

## 函数的注释

在函数前面增加一个字符串，将对函数的描述性文字写入其中，即为函数的注释。

当我们在帮助模式下查看函数的时候，会显示这段字符串。此外也可以在常规模式下使用 `@doc` 宏来查看它：

![图片描述](https://doc.shiyanlou.com/courses/uid310176-20200114-1578976791690/wm)

## 多返回值

所谓 “多返回值” 其实是不存在的，我们可以在 return 关键字后面写上多个返回值，将它们使用逗号隔开：

```julia
julia> function f()
           return 1, 3, 5
       end
f (generic function with 2 methods)

julia>
```

如上所示，调用函数 f 会返回 3 个值，其实这是假象，实际上函数的返回值永远只有一个，函数 f 的返回值看似是多个，实际上是一个元组，元组内有 3 个元素而已：

```julia
julia> function f()
           return 1, 3, 5
       end
f (generic function with 2 methods)

julia> x = f()
(1, 3, 5)

julia> x
(1, 3, 5)

julia> typeof(x)
Tuple{Int64,Int64,Int64}

julia>
```

## 匿名函数

所谓匿名函数，就是没有函数名，这样有一个小小的好处就是避免重复命名函数，在有些场景下匿名函数写起来和用起来更加方便。自定义匿名函数的格式是 `参数 -> 返回值` 。

举个例子，有这样一个函数 use_anonymous ，它接受两个参数：一个函数 func 和一个参数 arg ，返回值即为将 arg 作为 func 的参数调用 func ：

```julia
julia> function use_anonymous(func, arg)
           return func(arg)
       end
use_anonymous (generic function with 1 method)

julia> use_anonymous(x -> x ^ 2, 10)
100

julia>
```

如上所示，`x -> x ^ 2` 就是匿名函数，它将参数求平方后返回。

典型的匿名函数的使用场景就是调用 map 函数的时候。map 是 Julia 内置的函数，在许多其它编程语言中也有同名函数，且作用相同，不过返回值的数据类型可能稍有不同。map 的调用格式是 `map(函数, 被处理的参数)` ，与 use_anonymous 是差不多的，不过被处理的参数须为可遍历的容器数据类型，字符串、元组等，它的作用是使用第一个参数那个函数处理容器中的各个元素，然后将返回值写入一个元组中。

举例说明，作为参数的函数还是上面的那个求平方的匿名函数，第二个参数是被处理的元组：

```julia
julia> a = map(x -> x ^ 2, (1, 2, 3, 4, 5))
(1, 4, 9, 16, 25)

julia> a
(1, 4, 9, 16, 25)

julia> typeof(a)
NTuple{5,Int64}

julia>
```

匿名函数只是为了在某些场景下简化代码，它完全可以被具名函数替代的：

```julia
julia> function 求平方(x)
           return x ^ 2
       end
求平方 (generic function with 1 method)

julia> map(求平方, (1, 2, 3, 4, 5))
(1, 4, 9, 16, 25)

julia>
```

## 函数的参数

这一小节我们要了解默认参数、可变参数和关键字参数。

### 默认参数

有这样一个需求，要创建一个函数以求 Int 数据的多次方，这个函数将会被多次使用。这很简单：

```julia
julia> function f(x, n) # 求 x 的 n 次方
           x ^ n
       end
f (generic function with 2 methods)

julia> f(2, 4) # 2 的 4 次方
16

julia> f(3, 2) # 3 的 2 次方
9

julia>
```

如上所示，代码中的注释可以写到每一行的后面，使用 `#` 开头。如果这个函数最常被调用的是求平方，那么，我们可以将参数 n 设置一个默认值 2 ，这样在求平方的时候，就可以不需要提供参数 n 了：

```julia
julia> function f(x, n=2)
           x ^ n
       end
f (generic function with 3 methods)

julia> f(3)
9

julia> f(5)
25

julia> f(2, 4)
16

julia> f(3, 3)
27

julia>
```

如上所示，参数 n 就叫做 “默认参数”，因为它有一个默认值，调用函数 f 时可以不提供 n 的值，函数体内部将使用其默认值。

### 可变参数

有时我们有这样的需要，每次调用一个函数的时候，提供的参数数量不定，这就需要在定义函数时提供可变参数了。这种函数又叫做 “变参函数” ，即 “参数数量可变” 之意。

可变参数须写在普通参数后面，其特点是后面加三个点号 `...` 。

函数的参数也可以使用双冒号指定数据类型，这与指定函数返回值的数据类型一样。

现定义一个函数 f ：第一个参数是普通参数 a ，Int64 类型；第二个参数是可变参数 x ，不一定有几个值；返回值就是把 x 中每个元素都加上 a 组成新元组：

```julia
julia> function f(a::Int64, x...)::Tuple
           return map(x -> x + a, x)
       end
f (generic function with 5 methods)

julia> result = f(2, 10, 20, 30)
Tuple{Int64,Int64,Int64}
(12, 22, 32)

julia> result
(12, 22, 32)

julia> typeof(result)
Tuple{Int64,Int64,Int64}

julia>
```

可变参数 x 可以提供任意数量的参数，包括零个，在函数体内部，x 其实是一个元组。

如果本来就有一个元组 `t = (10, 20, 30)` ，如何将其作为可变参数调用函数 f 呢？在 t 后面加三个点：

```julia
julia> t = (10, 20, 30)
(10, 20, 30)

julia> f(2, t...)
Tuple{Int64,Int64,Int64}
(12, 22, 32)

julia>
```

### 关键字参数

所谓关键字参数，其实就是类似于字典那样的键值对作为参数，这么做的好处是参数可以带参数名，在函数体内部调用函数名即可得到对应的值。关键字参数也是要写在最后面，且要用分号 `;` 与前面的普通参数隔开，并且要加三个点号 `...` 。

举例说明，创建一个函数打印学生信息，需要提供学生的 name 、gender 、height 、grade 和 address 等信息：

```julia
julia> function print_student(name; args...)
           println("name: $(name)")
           for (key, value) in args
               println("$(key): $(value)")
           end
       end
print_student (generic function with 2 methods)

julia> print_student("小明", age=11, gender="M", height=120, addr="阿拉斯加")
name: 小明
age: 11
gender: M
height: 120
addr: 阿拉斯加

julia>
```

如上所示，args 这个参数可以提供一系列带函数名的参数，且数量不限，我们可以使用 for 循环获取其中的 key 和 value 。

## 总结

本节实验我们学习了函数的相关知识，包括如何定义函数、函数的返回值、为函数增加注释、匿名函数、函数多返回值的本质、函数的三种特殊参数等知识，希望大家多多练习，熟练掌握。

下一节实验我们将学习流程控制，其中包括本节实验我们已经在用的 for 循环。
