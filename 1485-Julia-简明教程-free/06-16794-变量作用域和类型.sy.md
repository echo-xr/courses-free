---
show: step
version: 1.0
enable_checker: false
---

# 变量作用域和类型

## 实验介绍

本节实验内容较少，主要学习两个知识点，就是变量的作用域和类型，了解 Julia 中有哪些特别之处。例如 while 循环体内部要使用外部变量就需要先声明。

#### 知识点

- 作用域结构
- local 关键字
- global 关键字
- 类型声明
- 类型转换
- 类型提升

## 作用域结构

首先认识一个概念，叫 “作用域块”，其实就是前面实验中提到的函数体、循环体之类的。Julia 中的作用域块有：

- function 函数
- while / for 循环
- try / catch 处理异常
- let / type

前三组我们都已经接触过，最后的 let 和 type 在后面会学习到。

代码通常离不开这些作用域块，它们以并列和嵌套的方式存在，例如两个相对独立的函数或函数内部使用 for 循环。变量作用域就指的是这些作用域块，变量的有效范围通常被限制在作用域块内。

我们可以把代码结构看成一个个作用域，它们有时是并列摆放的，有时是嵌套摆放的。给变量划分作用域有助于解决变量命名冲突，这是大多数编程语言的基本设置。这个概念是符合直觉的：两个函数可能同时都有叫做 x 的参数，而这两个 x 并不指向同一个东西。相似地，也有很多其它的情况下代码的不同块会使用同样名字而并不指向同一个东西。相同的变量名是否指向同一个东西的规则被称为作用域规则。

从有效范围来看，变量可以分为全局变量和局部变量，不过这也是相对而言的，举例说明：

```julia
julia> function f()
           a = 3
           while a > 0
               println(a)
               a -= 1
           end
       end
f (generic function with 2 methods)

julia> f()
3
2
1

julia> a
ERROR: UndefVarError: a not defined

julia>
```

如上所示，函数 f 的内部定义了一个变量 a ，在函数体内部，它就是全局变量，while 循环中可以直接调用它。但是函数体外部是没有这个变量的，对于整个交互解释器环境来说，函数 f 内部的变量 a 是局部变量，在函数外部无效。

当变量被引入到一个作用域中时，所有的内部作用域都继承了这个变量，除非某个内部作用域显式复写了它。将新变量引入当前作用域的方法：

- 声明 `local x` 或 `const x` ，可以引入新本地变量
- 声明 `global x` 使得 `x` 引入当前作用域和更内层的作用域
- 函数的参数，作为新变量被引入函数体的作用域
- 无论是在当前代码之前或 之后 ， `x = y` 赋值都将引入新变量 `x` ，除非 `x` 已经在任何外层作用域内被声明为全局变量或被引入为本地变量

下面例子中，循环内部和外部，仅有一个 `x` 被赋值：

```julia
julia> function foo(n)
           x = 0
           for i in 1: n
               x += 1
           end
           println(x)
       end
foo (generic function with 1 method)

julia> foo(3)
3

julia> foo(10)
10

julia>
```

也就是说，在 foo 函数内部，只有一个变量 x ，相对而言它就是全局变量，在 for 循环中此变量的值被修改，最后打印的是循环结束后被修改的变量 x 的值。

### local 关键字

在 for 循环体内部（while 也一样）定义的变量其作用范围仅限于循环体内部，举例说明：

```julia
julia> function foo(n)
           for i in 1: n
               println(i)
           end
           println(i)
       end
foo (generic function with 1 method)

julia> foo(3)
1
2
3
ERROR: UndefVarError: i not defined
Stacktrace:
 [1] foo(::Int64) at ./REPL[8]:5
 [2] top-level scope at REPL[9]:1

julia>
```

如上所示，函数倒数第二行代码抛出 UndefVarError 未定义变量错误，说明函数中没有变量 i ，只有 for 循环内才有。

虽然无法将 for 循环内部的变量 i 的作用域提升到整个函数，不过可以定义一个函数作用域 x 并将 i 赋值给它：

```julia
julia> function foo(n)
           x = 0
           for i in 1: n
               println(i)
               x = i
           end
           println("x = $x")
       end
foo (generic function with 1 method)

julia> foo(5)
1
2
3
4
5
x = 5

julia>
```

如果要做到全局变量在 for 循环体内部被修改后，不影响此变量在函数内的值，也是可以做到的，将其在 for 循环中设置为局部变量即可，使用 local 关键字来实现：

```julia
julia> function foo(n)
           x = 0
           for i in 1: n
               local x
               x = i
               println(x)
           end
           println("x = $x")
       end
foo (generic function with 1 method)

julia> foo(5)
1
2
3
4
5
x = 0

julia>
```

如上所示，函数在运行过程中，for 循环内部的变量 x 因为 `local x` 语句变成了局部变量，其有效区域被限制在 for 循环内部，因此倒数第二行代码打印的 x 的值仍是 0 。

在非顶层作用域给全局变量赋值的唯一方法，是在某个作用域中显式声明变量是全局的。否则，赋值会引入新的局部变量，而不是给全局变量赋值。

举例说明：

```julia
julia> function foo(n)
         f = y -> n + x + y
         x = 1
         println(f(2))
       end
foo (generic function with 1 method)

julia> foo(1)
4

julia>
```

如上所示，`f = y -> n + x + y` 是将等号后面的嵌套函数赋值给变量 f ，不过不需要预定义变量 n 和 x 的值，第三行 `x = n` 定义的变量 x 的作用范围包括函数 f ，所以可以在第 4 行调用函数 f ，此时函数 f 内的变量 n 和 x 就是全局变量。改写函数 foo 如下：

```julia
julia> function foo(n, x)
           f = y -> n + x + y
           println(f(2))
       end
foo (generic function with 2 methods)

julia> foo(3, 5)
10

julia>
```

如上所示，第三行代码调用 f 时，foo 函数内部的嵌套函数 f 中的参数 n 和 x 的值来自 foo 的参数，对于 f 而言，n 和 x 就是全局变量。

Julia 提供了内置函数 iseven 和 isodd 分别判断一个数值是不是偶数和奇数：

```julia
julia> iseven(3)
false

julia> isodd(3)
true

julia>
```

我们可以自定义一组互相递归验证的两个函数 even 和 odd 来实现这个判断功能：

```julia
julia> even(n) = n == 0 ? true  :  odd(n-1)
even (generic function with 1 method)

julia> odd(n)  = n == 0 ? false : even(n-1)
odd (generic function with 1 method)

julia> even(3)
false

julia> odd(3)
true

julia>
```

如上所示，函数在定义时，内部使用的变量可以未定义。

这两个函数均返回布尔值，等号后面即是函数的返回值，返回值是判断结果，参数 n 会一直被做减一操作，循环调用 odd 和 even 函数，直至在某一个函数中返回布尔值。

### global 关键字

再看下面的例子：

```julia
julia> for i in 1: 10
           y = 10
       end

julia> y
ERROR: UndefVarError: y not defined

julia> y = 1
1

julia> for i in 1: 10
           y = 10
       end

julia> y
1

julia>
```

如上所示，在交互解释器中未定义变量 y ，此时执行 for 循环语句，for 循环内部的变量 y 的作用域仅限于循环内部，并非全局变量。当交互解释器中定义了变量 y 之后，再次运行 for 循环，循环内部的变量 y 依然是局部变量，如何将其作为全局变量呢？可以使用 global 关键字：

```julia
julia> y
1

julia> for i in 1: 10
           global y
           y = i
       end

julia> y
10

julia>
```

在声明全局变量时，可以直接赋值，甚至可以使用分号声明多个变量：

```julia
julia> name
ERROR: UndefVarError: name not defined

julia> age
ERROR: UndefVarError: age not defined

julia> school
ERROR: UndefVarError: school not defined

julia> foo() = global name="小明"; age=12; school="No.1 Middle School"
"No.1 Middle School"

julia> foo()
"小明"

julia> name
"小明"

julia> age
12

julia> school
"No.1 Middle School"

julia>
```

### let 关键字

`let` 语句提供了另一种引入变量的方法。`let` 语句每次运行都会声明新变量。`let` 语法接受由逗号隔开的赋值语句或者变量名。其使用格式如下：

```julia
  let var1 = value1, var2, var3 = value3
      code ...
  end
```

`let x = x` 是合乎语法的，因为这两个 `x` 变量不同。它先对右边的求值，然后再引入左边的新变量并赋值。

因为 begin 关键字不引入新作用域块，在其中使用 let 关键字引入新作用域块是有效的：

```julia
julia> val = begin
           local x = 1
           let
               local x = 2
           end
           x
       end
1

julia> val
1

julia>
```

这相当于在 begin 内部使用 let 关键字开辟了一块新的作用域，其中的变量是可以被有效限定作用域的。

## 类型

在第二节实验中提到，Julia 作为动态语言，如果变量的数据类型被省略，就可以是任意数据类型。设置数据类型可以显著提高性能和系统稳定性。

Julia 类型系统的特性是，具体类型不能作为具体类型的子类型，所有的具体类型都是最终的，它们可以拥有抽象类型作为父类型。

### 类型声明

`::` 运算符可以用来在程序中给表达式和变量附加类型注释。这样做有两个理由：

1. 作为断言，帮助确认程序是否正常运行
2. 给编译器提供额外类型信息，帮助提升性能

`::` 运算符放在表示值的表达式之后时读作 “前者是后者的实例”，它用来断言左侧表达式是否为右侧表达式的实例。如果右侧是具体类型，此类型应该是左侧的实例。如果右侧是抽象类型，左侧应是一个具体类型的实例的值，该具体类型是这个抽象类型的子类型。如果类型断言为假，将抛出异常，否则，返回左值。

举例说明：

```julia
julia> (1 + 3)::Float64
ERROR: TypeError: in typeassert, expected Float64, got Int64
Stacktrace:
 [1] top-level scope at REPL[2]:1

julia> (1 + 3)::Int64
4

julia>
```

在 “函数” 一节中我们已经学习了类型声明通常用在函数参数和返回值上：

```julia
julia> function f(n::Int64)::Float64
           return n ^ 2
       end
f (generic function with 1 method)

julia> result = f(3)
9.0

julia> typeof(result)
Float64

julia>
```

### 类型转换

在创建变量时可以使用双冒号进行数据类型声明，以转换数据类型：

```julia
julia> function foo(a, b)
           x::Int8 = a
           y::Int8 = b
           x * y
       end
foo (generic function with 1 method)

julia> result = foo(2, 3)
6

julia> result
6

julia> typeof(result)
Int8

julia>
```

其中变量 x 和 y 在赋值时会自动转换数据类型为 Int8 ，所以结果返回值就是 Int8 类型。

使用内置函数 convert 也可以进行数据类型转换：

```julia
julia> s = 10
10

julia> typeof(s)
Int64

julia> s = convert(Int8, s)
10

julia> typeof(s)
Int8

julia>
```

转换数据类型时要注意不要越界，例如 Int8 类型的取值范围是 -128 至 127 ，240 就不可以被转换成 Int8 类型：

```julia
julia> Int8(240)
ERROR: InexactError: trunc(Int8, 240)
Stacktrace:
 [1] throw_inexacterror(::Symbol, ::Type{Int8}, ::Int64) at ./boot.jl:560
 [2] checked_trunc_sint at ./boot.jl:582 [inlined]
 [3] toInt8 at ./boot.jl:597 [inlined]
 [4] Int8(::Int64) at ./boot.jl:707
 [5] top-level scope at REPL[16]:1

julia>
```

如果要把字符串类型的数值例如 `"23"` 转换成 Int 类型，可以使用 parse 方法：

```julia
julia> parse(Int8, "23")
23

julia>
```

### 类型提升

一个简单的例子：

```julia
julia> 1 + 3.2
4.2

julia>
```

当 Int 类型和 Float 类型的数值进行运算时，会先将 Int 类型提升为 Float 类型，然后进行计算，这就是 “类型提升” 。

定义一个元组 t ，其中包含 Int 类型和 Float 类型的数值：

```julia
julia> t = (1, 3.14, Int16(9))
(1, 3.14, 9)

julia> typeof(t)
Tuple{Int64,Float64,Int16}

julia>
```

使用 promote 函数将所有元素提升为 Float 类型：

```julia
julia> t = (1, 3.14, Int16(9))
(1, 3.14, 9)

julia> t = promote(t...)
(1.0, 3.14, 9.0)

julia> typeof(t)
Tuple{Float64,Float64,Float64}

julia>
```

这里使用 `t...` 作为 promote 的参数，它就是我们前面讲到的可选参数。

同样我们可以使用加号作为函数求和：

```julia
julia> t
(1.0, 3.14, 9.0)

julia> +(t...)
13.14

julia>
```

## 总结

本节实验我们主要学习了变量作用域结构，如何使用 local 关键字定义局部变量、使用 global 定义全局变量，以及类型声明、类型转换和类型提升的知识。

希望大家通过以上实验的学习，对 Julia 语法有一个大概的了解。我们的课程作为入门课，未深入涉及 Julia 的复杂知识，如果大家能够通过我们的课程作为基础去自学更复杂的进阶内容，那便是再好不过了。
